plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.8-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "org.openapi.generator" version "6.2.1"
	id "com.thinkimi.gradle.MybatisGenerator" version "2.1.2"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

mybatisGenerator {
	verbose = true
	// 設定ファイル
	configFile = "${projectDir}/src/main/resources/mybatis/generatorConfig.xml"

	dependencies {
		mybatisGenerator 'com.itfsw:mybatis-generator-plugin:1.3.7'
		mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.7'
		mybatisGenerator 'mysql:mysql-connector-java:8.0.30'
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	mybatisGenerator
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
	implementation  'org.mybatis.generator:mybatis-generator-core:1.4.1'
	implementation 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.4.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.2'
	implementation "io.swagger.core.v3:swagger-annotations:2.1.10"
	compileOnly 'org.projectlombok:lombok:1.18.20'
	compileOnly 'io.swagger:swagger-annotations:1.6.5'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

tasks.named('test') {
	useJUnitPlatform()
}

task buildApiDoc(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
	generatorName = "html2"
	inputSpec = "$rootDir/src/main/resources/api-generator/schema.yaml".toString()
	outputDir = "$buildDir/apidoc".toString()
}

task buildSpringServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/api-generator/schema.yaml".toString()
	outputDir = "$rootDir".toString()
	apiPackage = "com.example.merikuri.generated.controller"
	modelPackage = "com.example.merikuri.generated.model"
	configOptions = [
			dateLibrary: "java8",
			interfaceOnly: "true",
			hideGenerationTimestamp: "true",
			skipDefaultInterfase: "true",
			booleanGetterPrefix: "is",
			useTags: "true",
			ignoreFileOverride: ".openapi-generator-ignore"
	]
}

compileJava.dependsOn tasks.buildSpringServer
sourceSets.main.java.srcDir "$buildDir/spring/src/main/java"